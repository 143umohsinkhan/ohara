/*
 * Copyright 2019 is-land
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "$rootDir/gradle/dependencies.gradle"

/**
 * Noted: this module should NOT have any production code!!! by chia
 */
dependencies {
  /**
   * scala plugin complains that we don't put scala library in production scope... by chia
   */
  implementation libs.scalaLibrary
  implementation libs.scalaReflect
  implementation project(':ohara-common')
  implementation project(':ohara-client')
  implementation project(':ohara-kafka')
  implementation project(':ohara-configurator')
  implementation project(':ohara-connector')
  implementation project(':ohara-agent')
  implementation project(':ohara-testing-util')
  implementation project(':ohara-metrics')
  implementation project(':ohara-streams')
  implementation project(':ohara-shabondi')
  implementation libs.kafkaConnectRuntime
  testImplementation libs.scalaLogging
  testImplementation libs.akkaHttpSprayJson
  // our it env use postgresql
  testImplementation libs.postgresql

  /**
   * Jar conflict between Hadoop & Kafka:
   *    * Jersey: Hadoop use jersey 1.x, Kafka use jersey 2.x
   *        * Solve running Kafka Connect mini cluster failed. OHARA-103
   *    * Jetty: Hadoop use org.mortbay.jetty 6.1.x, Kafka use use newer version(org.eclipse.jetty)
   *        * Exclude older release.
   *    * Zookeeper: Hadoop use Zookeeper 3.4.6, Kafka use newer version
   *        * Exclude older release.
   */
  testImplementation(libs.hadoopCommon) {
    exclude group: 'com.sun.jersey', module: 'jersey-core'
    exclude group: 'com.sun.jersey', module: 'jersey-json'
    exclude group: 'com.sun.jersey', module: 'jersey-servlet'
    exclude group: 'com.sun.jersey', module: 'jersey-server'
    exclude group: 'org.mortbay.jetty', module: 'jetty'
    exclude group: 'org.mortbay.jetty', module: 'jetty-util'
    exclude group: 'org.apache.zookeeper', module: 'zookeeper'
  }
  testImplementation (libs.hadoopHDFS) {
    exclude group: 'com.sun.jersey', module: 'jersey-core'
    exclude group: 'com.sun.jersey', module: 'jersey-json'
    exclude group: 'com.sun.jersey', module: 'jersey-servlet'
    exclude group: 'com.sun.jersey', module: 'jersey-server'
    exclude group: 'org.mortbay.jetty', module: 'jetty'
    exclude group: 'org.mortbay.jetty', module: 'jetty-util'
  }
  testImplementation project(path: ':ohara-agent', configuration: 'tests')
  testImplementation project(path: ':ohara-client', configuration: 'tests')
  testImplementation project(path: ':ohara-common', configuration: 'tests')
  testImplementation project(path: ':ohara-configurator', configuration: 'tests')
  testImplementation project(path: ':ohara-connector', configuration: 'tests')
  testImplementation project(path: ':ohara-kafka', configuration: 'tests')
  testImplementation project(path: ':ohara-metrics', configuration: 'tests')
  testImplementation project(path: ':ohara-shabondi', configuration: 'tests')
  testImplementation project(path: ':ohara-streams', configuration: 'tests')
  testImplementation project(path: ':ohara-testing-util', configuration: 'tests')
  testImplementation libs.akkaTestKit
  testImplementation libs.akkaHttpTestKit
  testImplementation libs.scalatest
  testImplementation libs.junit
  testImplementation libs.akkaStream

  if (project.hasProperty("ohara.it.jar.folder")) {
    // Load jar file for JDBC driver test
    fileTree(project.getProperty("ohara.it.jar.folder")).visit { FileVisitDetails jar ->
      testImplementation files(jar.file.path)
    }
  }
}

/**
 * package only stream jar.
 */
task streamJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output
  include '**/DumbStream**.class'
  archiveName "ohara-it-stream.jar"
}

/**
 * package only source connector.
 */
task sourceJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output
  include '**/connector/DumbSource**.class', "**/connector/package*.class"
  archiveName "ohara-it-source.jar"
}

/**
 * package only sink connector.
 */
task sinkJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output
  include '**/connector/DumbSink**.class', "**/connector/package*.class"
  archiveName "ohara-it-sink.jar"
}

/**
 * a custom test for IT tasks. It initialize the arguments for all tasks.
 */
class OharaTest extends Test {
  OharaTest() {
    project.getProperties().forEach {
      k, v ->
        if (k.startsWith("ohara.it")) {
          environment k, v
        }
    }
    maxParallelForks = 1
    forkEvery = 1
    minHeapSize = minHeapSize
    maxHeapSize = "4096m"
    testLogging {
      events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
    // generate the testing-purpose jars for IT
    dependsOn 'sourceJar'
    dependsOn 'sinkJar'
    dependsOn 'streamJar'
  }
}

/**
 * the junit arguments to test is controlled by root/gradle so we just assign the properties.
 */
test {
  project.getProperties().forEach {
    k, v ->
      if (k.startsWith("ohara.it")) {
        environment k, v
      }
  }
  // generate the testing-purpose jars for IT
  dependsOn 'sourceJar'
  dependsOn 'sinkJar'
  dependsOn 'streamJar'
}

def classOfCollieGroup = "com.island.ohara.it.category.CollieGroup"
def classOfConnectorGroup = "com.island.ohara.it.category.ConnectorGroup"
def classOfClientGroup = "com.island.ohara.it.category.ClientGroup"
def classOfStreamGroup = "com.island.ohara.it.category.StreamGroup"
def classOfPerformanceGroup = "com.island.ohara.it.category.PerformanceGroup"

task collieIT(type: OharaTest) {
  useJUnit {
    includeCategories classOfCollieGroup
  }
}

task connectorIT(type: OharaTest) {
  useJUnit {
    includeCategories classOfConnectorGroup
  }
}

task clientIT(type: OharaTest) {
  useJUnit {
    includeCategories classOfClientGroup
  }
}

task streamIT(type: OharaTest) {
  useJUnit {
    includeCategories classOfStreamGroup
  }
}

task performance(type: OharaTest) {
  useJUnit {
    includeCategories classOfPerformanceGroup
  }
}

task otherIT(type: OharaTest) {
  useJUnit {
    excludeCategories classOfCollieGroup,
            classOfConnectorGroup,
            classOfClientGroup,
            classOfStreamGroup,
            classOfPerformanceGroup
  }
}

jar.dependsOn sourceJar
jar.dependsOn sinkJar
jar.dependsOn streamJar