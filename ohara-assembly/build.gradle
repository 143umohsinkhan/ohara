apply from: "$rootDir/gradle/dependencies.gradle"
apply plugin: 'java-library-distribution'
dependencies {
  // using compile enable java-library-distribution to include all dependencies in binary file
  compile project(':ohara-common')
  compile project(':ohara-client')
  compile project(':ohara-kafka')
  compile project(':ohara-hdfs-connector')
  compile project(':ohara-ftp-connector')
  compile project(':ohara-jdbc-connector')
  compile project(':ohara-perf-connector')
  compile project(':ohara-configurator')
  // the following dependencies is required in runtime
  compile libs.postgresql
  compile libs.akkaSlf4j
  compile libs.scalaLogging
  testImplementation project(path: ':ohara-agent', configuration: 'tests')
  testImplementation project(path: ':ohara-client', configuration: 'tests')
  testImplementation project(path: ':ohara-common', configuration: 'tests')
  testImplementation project(path: ':ohara-configurator', configuration: 'tests')
  testImplementation project(path: ':ohara-demo', configuration: 'tests')
  testImplementation project(path: ':ohara-ftp-connector', configuration: 'tests')
  testImplementation project(path: ':ohara-hdfs-connector', configuration: 'tests')
  // ohara-http is not in maintain mode currently, we should not check this module by now -- by Sam
  // testImplementation project(path: ':ohara-http', configuration: 'tests')
  testImplementation project(path: ':ohara-it', configuration: 'tests')
  testImplementation project(path: ':ohara-jdbc-connector', configuration: 'tests')
  testImplementation project(path: ':ohara-kafka', configuration: 'tests')
  testImplementation project(path: ':ohara-perf-connector', configuration: 'tests')
  testImplementation project(path: ':ohara-streams', configuration: 'tests')
  testImplementation project(path: ':ohara-testing-util', configuration: 'tests')
  testImplementation libs.akkaHttpTestKit
  testImplementation libs.scalatest
  testImplementation libs.junit
}

task uberJar(type: Jar) {
  classifier = 'uber'
  // TODO: should we exclude kafka-related jars?
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}
// Both assembly tasks ought to depend on ohara-manager's build phase since ohara-assembly needs to package the
// dependencies of ohara-manager, and the dependencies of ohara-manager is generated by build task.
distZip.dependsOn(':ohara-manager:build')
distTar.dependsOn(':ohara-manager:build')

distributions {
  main {
    baseName = "ohara"
    contents {
      exclude "ohara-assembly-${version}.jar"
      /**
       * exclude useless dirs and files from ohara-manager
       */
      exclude ".env"
      exclude ".env.test"
      exclude ".eslintrc"
      exclude ".gitignore"
      exclude ".eslintignore"
      exclude ".prettierignore"
      exclude ".build.gradle"
      exclude ".yarnrc"
      exclude "package.json"
      exclude "cypress.json"
      exclude "yarn.lock"
      exclude "node_modules"
      exclude "cypress"
      exclude "public"
      exclude "coverage"
      exclude "src"

      into("conf") {
        // ref to root module
        from { "$projectDir/../conf" }
      }
      into("bin") {
        // ref to root module
        from { "$projectDir/../bin" }
        fileMode 0755
      }
      into("manager/client") {
        from { "$projectDir/../ohara-manager/client" }
      }
      into("manager/constants") {
        from { "$projectDir/../ohara-manager/constants" }
      }
      into("manager/node_modules") {
        from { "$projectDir/../ohara-manager/node_modules" }
      }
      into("manager/routes") {
        from { "$projectDir/../ohara-manager/routes" }
      }
      into("manager/utils") {
        from { "$projectDir/../ohara-manager/utils" }
      }
      into("manager") {
        from { "$projectDir/../ohara-manager/config.js" }
      }
      into("manager") {
        from { "$projectDir/../ohara-manager/index.js" }
      }
      into("manager") {
        from { "$projectDir/../gradle.properties" }
      }
    }
  }
}
