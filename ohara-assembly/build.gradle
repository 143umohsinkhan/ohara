apply from: "$rootDir/gradle/dependencies.gradle"
apply plugin: 'java-library-distribution'
apply plugin: 'scala'
dependencies {
  // using compile enable java-library-distribution to include all dependencies in binary file
  compile project(':ohara-common')
  compile project(':ohara-kafka')
  compile project(':ohara-hdfs-connector')
  compile project(':ohara-ftp-connector')
  compile project(':ohara-jdbc-connector')
  compile project(':ohara-configurator')
  // the following dependencies is required in runtime
  compile libs.akkaSlf4j
  compile libs.scalaLogging
  testImplementation project(path: ':ohara-common', configuration: 'tests')
  testImplementation project(path: ':ohara-kafka', configuration: 'tests')
  testImplementation project(path: ':ohara-testing-util', configuration: 'tests')
  testImplementation project(path: ':ohara-hdfs-connector', configuration: 'tests')
  testImplementation project(path: ':ohara-configurator', configuration: 'tests')
  testImplementation libs.scalatest
  testImplementation libs.junit
}

distributions {
  main {
    baseName = "ohara"
    contents {
      exclude "ohara-assembly-${version}.jar"
      /**
       * exclude useless files from ohara-manager
       */
      exclude "node_modules"
      exclude "cypress"
      exclude "build"
      exclude "cypress.json"
      exclude ".env.test"
      exclude ".eslintrc"
      exclude ".gitignore"
      exclude ".eslintignore"
      exclude ".prettierignore"
      exclude ".build.gradle"
      into("conf") {
        // ref to root module
        from { "$projectDir/../conf" }
      }
      into("bin") {
        // ref to root module
        from { "$projectDir/../bin" }
        fileMode 0755
      }
      into("manager/client") {
        from { "$projectDir/../ohara-manager/client" }
      }
      into("manager/constants") {
        from { "$projectDir/../ohara-manager/constants" }
      }
      into("manager/routes") {
        from { "$projectDir/../ohara-manager/routes" }
      }
      into("manager/utils") {
        from { "$projectDir/../ohara-manager/utils" }
      }
      into("manager") {
        from { "$projectDir/../ohara-manager/index.js" }
      }
      into("manager") {
        from { "$projectDir/../ohara-manager/package.json" }
      }
    }
  }
}