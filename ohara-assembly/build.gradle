/*
 * Copyright 2019 is-land
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "$rootDir/gradle/dependencies.gradle"
apply plugin: 'java-library-distribution'
dependencies {
  // using compile enable java-library-distribution to include all dependencies in binary file
  compile project(':ohara-client')
  compile project(':ohara-common')
  compile project(':ohara-configurator')
  compile project(':ohara-connector')
  compile project(':ohara-kafka')
  compile project(':ohara-metrics')
  compile project(':ohara-shabondi')
  compile project(':ohara-streams')
  compile libs.akkaSlf4j
  testImplementation project(path: ':ohara-agent', configuration: 'tests')
  testImplementation project(path: ':ohara-client', configuration: 'tests')
  testImplementation project(path: ':ohara-common', configuration: 'tests')
  testImplementation project(path: ':ohara-configurator', configuration: 'tests')
  testImplementation project(path: ':ohara-connector', configuration: 'tests')
  testImplementation project(path: ':ohara-it', configuration: 'tests')
  testImplementation project(path: ':ohara-kafka', configuration: 'tests')
  testImplementation project(path: ':ohara-metrics', configuration: 'tests')
  testImplementation project(path: ':ohara-shabondi', configuration: 'tests')
  testImplementation project(path: ':ohara-streams', configuration: 'tests')
  testImplementation project(path: ':ohara-testing-util', configuration: 'tests')
  testImplementation libs.scalaLogging
  testImplementation libs.akkaHttpTestKit
  testImplementation libs.scalatest
  testImplementation libs.junit
  testImplementation libs.kafkaStream
  testImplementation libs.mockito
  testImplementation libs.ftpServer
  testImplementation libs.commonsLang
}

task uberJar(type: Jar) {
  classifier = 'uber'
  // TODO: should we exclude kafka-related jars?
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}
// Both assembly tasks ought to depend on ohara-manager's build phase since ohara-assembly needs to package the
// dependencies of ohara-manager, and the dependencies of ohara-manager is generated by build task.
distZip.dependsOn(':ohara-manager:build')
distTar.dependsOn(':ohara-manager:build')

distributions {
  main {
    baseName = "ohara"
    contents {
      exclude "ohara-assembly-${version}.jar"
      /**
       * exclude useless dirs and files from ohara-manager
       */
      exclude ".env"
      exclude ".env.test"
      exclude ".eslintrc"
      exclude ".gitignore"
      exclude ".eslintignore"
      exclude ".prettierignore"
      exclude ".build.gradle"
      exclude ".yarnrc"
      exclude "package.json"
      exclude "cypress.json"
      exclude "yarn.lock"
      exclude "node_modules"
      exclude "cypress"
      exclude "public"
      exclude "coverage"
      exclude "src"

      into("conf") {
        // ref to root module
        from { "$projectDir/../conf" }
      }
      into("bin") {
        // ref to root module
        from { "$projectDir/../bin" }
        fileMode 0755
      }
      into("manager/client") {
        from { "$projectDir/../ohara-manager/client" }
      }
      into("manager/constants") {
        from { "$projectDir/../ohara-manager/constants" }
      }
      into("manager/node_modules") {
        from { "$projectDir/../ohara-manager/node_modules" }
      }
      into("manager/routes") {
        from { "$projectDir/../ohara-manager/routes" }
      }
      into("manager/utils") {
        from { "$projectDir/../ohara-manager/utils" }
      }
      into("manager") {
        from { "$projectDir/../ohara-manager/config.js" }
      }
      into("manager") {
        from { "$projectDir/../ohara-manager/start.js" }
      }
      into("manager") {
        from { "$projectDir/../gradle.properties" }
      }
    }
  }
}
