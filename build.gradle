plugins {
  id "com.diffplug.gradle.spotless" version "3.15.0"
}

allprojects {
  repositories {
    mavenCentral()
    maven { url "https://repository.cloudera.com/artifactory/cloudera-repos/" }
    maven { url "https://dl.bintray.com/cakesolutions/maven/" }
  }
}

ext {
  userMaxForks = project.hasProperty('maxParallelForks') ? maxParallelForks.toInteger() : null
}

subprojects {
  if (it.name != 'ohara-manager') {
    if (it.name == 'ohara-common' || it.name == 'ohara-testing-util')
      apply plugin: 'java'
    else apply plugin: 'scala'
    test {
      maxParallelForks = userMaxForks ?: Runtime.runtime.availableProcessors()/2 ?: 1
      minHeapSize = "256m"
      maxHeapSize = "4096m"
      testLogging {
        events = ["passed", "skipped", "failed"]
      }
    }
  }

  // ohara-manager doesn't generate test.jar
  if (it.name != 'ohara-manager') {
    configurations {
      tests.extendsFrom archives
    }
    task testJar(type: Jar, dependsOn: testClasses) {
      classifier = 'tests'
      from sourceSets.test.output
    }
    artifacts {
      archives testJar
    }
  }

  // All sub-projects have applied the scala checkstyle.
  if (it.name != 'ohara-manager') {
    apply plugin: "com.diffplug.gradle.spotless"
    if (it.name == 'ohara-common' || it.name == 'ohara-testing-util'||it.name=='ohara-kafka') {
      spotless {
        java {
          googleJavaFormat()
          removeUnusedImports()
        }
      }
    }else {
      spotless {
        scala {
          // DON'T change the scalafmt version since the rules may be changed in newer version.
          scalafmt("1.5.1").configFile("$rootDir/.scalafmt.conf")
        }
      }
    }
  }

  // we put some test case in ohara-assembly so those compiler options should be added to ohara-assembly also.
  tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = [
      // Scala Compiler Options
      // https://github.com/scala/scala/blob/2.12.x/src/compiler/scala/tools/nsc/settings/StandardScalaSettings.scala
      // https://github.com/scala/scala/blob/2.12.x/src/compiler/scala/tools/nsc/settings/ScalaSettings.scala
      "-deprecation",
      "-unchecked",
      "-encoding", "utf8",
      "-Xlog-reflective-calls",
      "-feature",
      "-language:postfixOps",
      "-language:implicitConversions",
      "-language:existentials",
      "-target:jvm-1.8",
      "-explaintypes",
      "-feature",
      "-unchecked",
      // -Xlint enables all -Xlint:xxx checks.
      // see https://github.com/scala/scala/blob/v2.11.12/src/compiler/scala/tools/nsc/settings/Warnings.scala#L102
      "-Xlint",
      "-Ywarn-dead-code",
      "-Ywarn-unused-import",
      "-Xfatal-warnings",
      // in order to enable SAM in scala 2.11.
      // see https://github.com/scala/scala/pull/3037
      // TODO: remove this flag after we are in scala 2.12.  by chia
      "-Xexperimental"
    ]

    configure(scalaCompileOptions.forkOptions) {
      memoryMaximumSize = '1g'
      jvmArgs = ['-XX:MaxMetaspaceSize=512m']
    }
  }
}


